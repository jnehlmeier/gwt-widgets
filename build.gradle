apply plugin: 'java-library'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_8
group = 'org.gwtproject.widgets'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://repo.vertispan.com/gwt-snapshot'
    }
}

sourceSets.main.java.srcDir 'src/tmp/java'
sourceSets.main.resources.srcDir 'src/tmp/resources'
sourceSets.main.java.srcDir 'src-generated/main/java'
sourceSets.test.java.srcDir 'src-generated/test/java'

dependencies {
    annotationProcessor 'org.gwtproject.uibinder:gwt-uibinder-processor:1.0.0-SNAPSHOT'
    // Why is that explicitly needed for gwt-uibinder-processor ?! 
    // Should be transitive dependency    
    annotationProcessor 'com.google.gwt:gwt-dev:2.8.2'
    annotationProcessor 'com.google.gwt:gwt-user:2.8.2'
    annotationProcessor 'org.gwtproject.safehtml:gwt-safehtml-template-processor:1.0-SNAPSHOT'
    api 'com.google.gwt:gwt-user:2.8.2'
    api 'org.gwtproject.core:gwt-core:1.0-SNAPSHOT'
    api 'org.gwtproject.event:gwt-event:HEAD-SNAPSHOT'
    api 'org.gwtproject.event:gwt-logical-event:HEAD-SNAPSHOT'
    api 'org.gwtproject.event:gwt-event-legacy:HEAD-SNAPSHOT'
    api 'org.gwtproject.event:gwt-event-dom:1.0-SNAPSHOT'
    api 'org.gwtproject:editors:1.0-SNAPSHOT'
    api 'org.gwtproject.animation:gwt-animation:1.0-SNAPSHOT'
    api 'org.gwtproject.user.window:gwt-window:HEAD-SNAPSHOT'
    api 'org.gwtproject.aria:gwt-aria:1.0-SNAPSHOT'
    api 'org.gwtproject.layout:gwt-layout:1.0-SNAPSHOT'
    api 'org.gwtproject.safehtml:gwt-safehtml:1.0-SNAPSHOT'
    api 'org.gwtproject.safecss:gwt-safecss:1.0-SNAPSHOT'
    api 'org.gwtproject.dom:gwt-dom:1.0-SNAPSHOT'
    api 'org.gwtproject.timer:gwt-timer:1.0-SNAPSHOT'
    api 'org.gwtproject.user.history:gwt-history:HEAD-SNAPSHOT'
    api 'org.gwtproject.uibinder:gwt-uibinder-client:1.0.0-SNAPSHOT'

    testAnnotationProcessor 'org.gwtproject.safehtml:gwt-safehtml-template-processor:1.0-SNAPSHOT'
    testImplementation 'junit:junit:4.12'
    testRuntimeOnly 'com.google.gwt:gwt-dev:2.8.2'
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.sourcepath = files('src/main/resources')
compileJava.options.annotationProcessorGeneratedSourcesDirectory = file('src-generated/main/java')
compileTestJava.options.encoding = 'UTF-8'
compileTestJava.options.annotationProcessorGeneratedSourcesDirectory = file('src-generated/test/java')

clean {
    delete file('src-generated/main/java')
    delete file('src-generated/test/java')
}

jar {
    from sourceSets.main.allJava
}

tasks.withType(Test) {
    enableAssertions = false
}

test {
    classpath += sourceSets.main.allJava.sourceDirectories + sourceSets.test.allJava.sourceDirectories
    systemProperty "gwt.args", "-draftCompile"
    include '**/*Suite.class'
    scanForTestClasses = false
}









